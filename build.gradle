plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
    id 'maven-publish'
}

version = project.mod_version + project.mod_suffix
group = project.maven_group
def buildnumber = 1

sourceSets {
    main {
        java {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.compileClasspath
        }
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven {
        url 'https://masa.dy.fi/maven'
    }
    maven { url 'https://jitpack.io' }
}

configurations {
    provided
    // Make compile extend from our provided configuration so that things added to bundled end up on the compile classpath
    implementation.extendsFrom(provided)
}

loom {
    accessWidenerPath = file("src/main/resources/spacebarcrafting.accesswidener")
}


dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0-rc.2")))

    provided group: 'org.apache.commons', name: 'commons-collections4', version: 'latest.release'

}


def placeholders = [
        'version': project.version,
        'mc_version': project.minecraft_version,
        'build': buildnumber
]

processResources {
    inputs.properties placeholders
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand placeholders
    }
    filesMatching("placeholders.properties") {
        expand placeholders
    }
}


def targetJavaVersion = 16
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    from configurations.provided.asFileTree.files.collect { zipTree(it) }
}

// configure the maven publication
publishing {
    if (System.getenv("CI")!=null){
        publications {
            maven(MavenPublication) {
                groupId = this.getGroup()
                artifactId = this.getName().toLowerCase()
                version = System.getenv("GITHUB_REF_NAME")
                from components.java
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/"+System.getenv("GITHUB_REPOSITORY")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

tasks.register("updateBuildNumber"){
    doLast {
        Properties props = new Properties()

        File propsFile = new File("build.properties")

        try {
            props.load(propsFile.newDataInputStream())

            String prevVersion = (props.getProperty('mod_version'))

            if (prevVersion.equals(project.mod_version)) {

                buildnumber = (((props.getProperty('mod_buildnumber')) as Integer) + 1)
                placeholders.build = buildnumber

            }
        }catch (IOException ignored){
        }

        props.setProperty('mod_buildnumber', buildnumber.toString())
        props.setProperty('mod_version', project.mod_version.toString())

        props.store(propsFile.newWriter(), null)

        props.load(propsFile.newDataInputStream())

    }
    outputs.upToDateWhen { false }
}

tasks.register("FinalizeBuild", Copy) {
    from "$buildDir/libs"
    into "output/libs"
    include "${project.archives_base_name}-${project.version}.jar"
    rename { String filename ->
        return "${project.archives_base_name}-${project.version} build ${buildnumber}.jar"
    }
    doLast {
        mkdir "output/libs"
        println "Output File:"
        println "${project.archives_base_name}-${project.version} build ${buildnumber}.jar"
    }
    outputs.upToDateWhen { false }
}

tasks.compileJava.finalizedBy updateBuildNumber
tasks.updateBuildNumber.shouldRunAfter compileJava
tasks.build.finalizedBy FinalizeBuild